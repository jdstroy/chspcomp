/***********************************************************

	HSP3コンパイラ "cHspComp" main file
	
***********************************************************/

/* コンパイルオプション */
#packopt name		"chspcomp"
#packopt runtime	"hsp3c.hrt"
#packopt hide		1

/* 定数 */
;#include "hspdef.as"
#include "h_mydef.hsp"

#define OPT_AUTOMAKE		"m"
#define OPT_DEBUG_INFO		"i"
#define OPT_DEBUG_SYMBOL	"d"
#define OPT_DEBUG_WIN		"w"
#define OPT_LAST_MES		"e"
#define OPT_OBJ_OBJ			"o"
#define OPT_OBJ_FILE		"c"
#define OPT_OBJ_START		"s"
#define OPT_KEYWORD			"k"
#define OPT_NO_MACRO		"h"

/* 汎用モジュール */
#include "gm_chspcomp.hsp"

/* アプリケーション部品モジュール */
#include "m_cmdline.hsp"
#include "m_custom_dialog.hsp"

	/********************/
	/* 主な変数のリスト */
	/********************/

;	filepath		; 処理対象のスクリプトファイルパスorオブジェクトファイルパス
;	runopt			; オブジェクトファイル実行時の起動オプション
;	objname			; コンパイルで作成するオブジェクトファイル名
;	dbsym_flag		; コンパイル時、デバッグシンボルを有効にするかどうか
;	dbinfo_flag		; コンパイル時、デバッグ情報を付加するかどうか
;	dbwin_flag		; コンパイル時、デバッグウィンドウ常時表示を有効にするかどうか
;	nomacro_flag	; コンパイル時、拡張マクロを有効にするかどうか


	/************************/
	/* 事前チェック、初期化 */
	/************************/

	/* 最後に行ったコンパイル時の出力メッセージを表示 */
	if chkopt(OPT_LAST_MES) {			; eオプション？
		ShowLastMesDialog
		end
	}
	/* HSPのキーワード一覧を表示 */
	if chkopt(OPT_KEYWORD) {			; kオプション？
		ShowSymListDialog
		end
	}

	/* スクリプト、またはオブジェクトファイルのパスを取得 */
	filepath	= getarg(0, TRUE@)		; 第1引数取得
	exist	filepath
	if (strsize = -1) {
		dialog	"指定されたパスのファイルは存在しません。", 1, ERR_MES_0@
		end
	}

	/* gm_chspcomp.hsp初期化 */
	chsc_ini	filepath, chkopt(OPT_AUTOMAKE)

	/* 拡張子チェック*/
	ext	= getpath(getpath(filepath, GETPATH_EXT@), GETPATH_LOWCASE@)
	if ((ext ! ".as") & (ext ! ".hsp")) {
		goto *_run
	}
	
	/**************/
	/* コンパイル */
	/**************/
*_comp
	/* オブジェクトファイル名セット */
	if chkopt(OPT_OBJ_FILE) {							; cオプション？
		objname	= ""
		goto *@f
	}
	if (chkopt(OPT_OBJ_START) | chkopt(OPT_AUTOMAKE)) {	; sオプション or mオプション？
		objname	= "start.ax"
		goto *@f
	}
	objname	= "obj"
*@

	/* コンパイルオプションセット */
	if chkopt(OPT_AUTOMAKE) {						; 実行ファイル作成
		dbsym_flag	= FALSE@							; デバッグモードシンボルON/OFF設定
		dbinfo_flag	= FALSE@							; デバッグ情報付加指定
		dbwin_flag	= FALSE@							; デバッグウィンドウ表示ON/OFF設定
	}
	else {											; その他
		dbsym_flag	= chkopt(OPT_DEBUG_SYMBOL)
		dbinfo_flag	= chkopt(OPT_DEBUG_INFO)
		dbwin_flag	= chkopt(OPT_DEBUG_WIN)
	}
	nomacro_flag	= chkopt(OPT_NO_MACRO)			; 拡張マクロON/OFF設定

	/* コンパイル実行 */
	chsc_autocomp	objname, dbsym_flag, dbinfo_flag, dbwin_flag, nomacro_flag
	if (stat = COMP_ERR@chspcomp) {					; 失敗
		ShowLastMesDialog
	}

	/* コンパイルのみの指定なら終了 */
	if (chkopt(OPT_OBJ_OBJ) | chkopt(OPT_OBJ_FILE) | chkopt(OPT_OBJ_START)) {
		dialog (objname + "を作成しました。"), , DLG_TITLE_1@
		end
	}

	/* 実行ファイル作成の指定があれば実行はなし */
	if chkopt(OPT_AUTOMAKE) {						; mオプション？
		goto *_make
	}
	
	/****************************/
	/* オブジェクトファイル実行 */
	/****************************/
*_run
	/* 起動オプション取得 */
	runopt	= ""
	repeat -1, 1
		prm	= getarg(cnt, FALSE@)
		if (prm = "") {
			break
		}
		if (cnt > 1) {
			runopt	+= " "
		}
		runopt	+= prm
	loop
	/* 実行 */
	chsc_run	runopt
	end

	/********************/
	/* 実行ファイル作成 */
	/********************/
*_make
	chsc_automake; TRUE@
	if (stat = MAKE_ERR@chspcomp) {
		ShowLastMesDialog
	}
	dialog ("実行ファイルを作成しました。"), , DLG_TITLE_1@
	end
